let AnomalyThreshold=1.2;
let baselineTimeRange=30d;
let requestThreshold=10;
let baselineSample=5m;
let avgRequestsPerMinute = 
    AzureDiagnostics
    | where Category contains "FrontDoorAccessLog"
    | where TimeGenerated > ago(baselineTimeRange)
    | extend requestUri_s = iff(indexof(requestUri_s,"?") == -1,requestUri_s,substring(requestUri_s,0,indexof(requestUri_s,"?")))
    | summarize requests_per_minute = count() by clientIp_s,requestUri_s, bin(TimeGenerated, baselineSample)
    | where requests_per_minute >= requestThreshold
    | summarize avgRequests = avg(requests_per_minute) by clientIp_s,requestUri_s
    | extend GeoInfo=geo_info_from_ip_address(clientIp_s)
;
AzureDiagnostics
| where Category contains "FrontDoorAccessLog"
| where TimeGenerated > ago(baselineSample)
| extend requestUri_s = iff(indexof(requestUri_s,"?") == -1,requestUri_s,substring(requestUri_s,0,indexof(requestUri_s,"?")))
| summarize requests_per_minute = count() by clientIp_s,requestUri_s, bin(TimeGenerated, baselineSample)
| join kind=inner   (
    avgRequestsPerMinute
    )
    on clientIp_s,requestUri_s
| where requests_per_minute > avgRequests * AnomalyThreshold
//| where isnotempty(requestUri_s1)
| project-away *1
